# Info.plist
set(INFO_PLIST_IN_PATH ${CMAKE_CURRENT_SOURCE_DIR}/Info.plist.in)
set(INFO_PLIST_PATH ${CMAKE_CURRENT_BINARY_DIR}/Info.plist)
configure_file(${INFO_PLIST_IN_PATH} ${INFO_PLIST_PATH})

set(APP_RESOURCES
    Main.storyboard
    Cannon_Exterior.hdr
)

file(GLOB_RECURSE SOURCES "*.m")
file(GLOB_RECURSE HEADERS "*.h")
add_executable(example MACOSX_BUNDLE
    ${HEADERS}
    ${SOURCES}
    ${APP_RESOURCES}
)

find_library(COCOA_LIBRARY Cocoa)
find_library(FOUNDATION_LIBRARY Foundation)
find_library(SCENEKIT_LIBRARY SceneKit)
find_library(METAL_LIBRARY Metal)
find_library(METALKIT_LIBRARY MetalKit)
find_library(UTI_LIBRARY UniformTypeIdentifiers)

target_link_libraries(example GLTF2SceneKit ${COCOA_LIBRARY} ${FOUNDATION_LIBRARY} ${SCENEKIT_LIBRARY} ${METAL_LIBRARY} ${METALKIT_LIBRARY} ${UTI_LIBRARY})

target_include_directories(example PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_BINARY_DIR}
)

set_target_properties(example PROPERTIES
    RESOURCE "${APP_RESOURCES}"
    BUILD_WITH_INSTALL_RPATH TRUE
    INSTALL_RPATH @executable_path/../Frameworks
    MACOSX_BUNDLE_INFO_PLIST ${INFO_PLIST_PATH}
    XCODE_ATTRIBUTE_CLANG_ENABLE_OBJC_ARC YES
    XCODE_ATTRIBUTE_INFOPLIST_FILE ${INFO_PLIST_PATH}
    XCODE_EMBED_FRAMEWORKS_CODE_SIGN_ON_COPY TRUE
    XCODE_EMBED_FRAMEWORKS_REMOVE_HEADERS_ON_COPY TRUE
    XCODE_ATTRIBUTE_MACOSX_DEPLOYMENT_TARGET 11.0
)

source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES
    ${HEADERS}
    ${SOURCES}
    ${APP_RESOURCES}
)
